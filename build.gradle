plugins {
    id 'checkstyle'
    id 'java-library'
    id 'distribution'
    id 'maven-publish'
    id "kr.motd.sphinx" version "2.3.1"
}

apply plugin: 'base'
apply plugin: 'java'
group 'com.opendxl.streaming'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.github.stefanbirkner:system-rules:1.19.0'
    testImplementation 'com.github.tomakehurst:wiremock-jre8:2.23.2'
    testImplementation 'org.mockito:mockito-all:1.10.19'
    implementation 'com.e-movimento.tinytools:privilegedaccessor:1.2.2'
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'org.apache.httpcomponents:httpclient:4.5.5'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'log4j:log4j:1.2.17'
}

// Jar Manifest info
jar {
    manifest {
        attributes 'Main-Class': 'com.opendxl.streaming.cli.CommandLineInterface',
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        doFirst {
            from {
                configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
                configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
            }
        }
        baseName = rootProject.name
    }
}

// Task for creating a jar with the source files
task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    baseName = rootProject.name

}

// Task for creating a jar with JavaDoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    baseName = rootProject.name
}

// Property with the source file list used by sourceZip and sourceTarGz tasks to respectively create a zip file and
// a tar.gz file containing all source files required to build the project
ext {
    sourceTree = fileTree(dir: projectDir)
    sourceTree.include 'src/**/*'
    sourceTree.include 'config/**/*'
    sourceTree.include 'gradle/**/*'
    sourceTree.include 'gradle*'
    sourceTree.include '.gitignore'
    sourceTree.include '.travis.yml'
    sourceTree.include 'LICENSE'
    sourceTree.include 'README*'
    sourceTree.include 'build.gradle'
    sourceTree.include 'settings.gradle'
    blank = ''
}

// Task for creating a zip compressed file containing all necessary source files to build the project
task sourceZip(type: Zip) {
    classifier = project.ext.blank
    baseName = project.ext.blank
    appendix = project.ext.blank
    from project.ext.sourceTree

    doLast {
        println "Path to zip: $archivePath.path"
    }
}

// Task for creating a tar.gz compressed file containing all necessary source files to build the project
task sourceTarGz(type: Tar) {
    compression = Compression.GZIP
    extension = 'tar.gz'
    classifier = project.ext.blank
    baseName = project.ext.blank
    appendix = project.ext.blank
    from project.ext.sourceTree

    doLast {
        println "Path to tar.gz: $archivePath.path"
    }
}

javadoc {
    source = sourceSets.main.allJava
    options.memberLevel = JavadocMemberLevel.PUBLIC
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
    archives sourceZip
    archives sourceTarGz
}

// Set up the distribution zip
distributions {
    main {
        baseName = 'opendxlstreamingclient-java-sdk'
        contents {
            from(jar) {
                into('lib')
            }
            from(sphinx) {
                into('doc')
            }
            from(javadoc) {
                into('doc/javadoc')
            }
            from('distribution')
        }

    }
}




// Make check style run before creating a jar
jar.dependsOn(checkstyleMain)

// Force distZip to be dependent on javadocJar and sourceJar
distZip.dependsOn javadocJar
distZip.dependsOn sourceJar

sphinx {
    // Change the source directory.
    sourceDirectory = "${projectDir}/docs"
    // Change the output directory.
    outputDirectory = "${project.buildDir}/docs"
}

task versionFile()  {
    doLast {
        new File("${projectDir}/VERSION").text = """$version"""
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            pom {
                name = 'OpenDXL Streaming Java Client'
                description = 'An OpenDXL streaming client library for Java'
                url = 'https://github.com/opendxl/opendxl-streaming-client-java'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        email = 'support@opendxl.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/opendxl/opendxl-streaming-client-java.git'
                    developerConnection = 'scm:git:ssh://github.com/opendxl/opendxl-streaming-client-java.git'
                    url = 'https://github.com/opendxl/opendxl-streaming-client-java'
                }
            }

            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

        }
    }

    repositories {
        maven {
            url = "$buildDir/repo"

//            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
//            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
//            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
//            credentials {
//                username sonatypeUsername
//                password sonatypePassword
//            }
        }
    }

}

distZip.dependsOn javadocJar
distZip.dependsOn sourceJar
sphinx.dependsOn(versionFile)